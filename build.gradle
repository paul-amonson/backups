// Copyright (C) 2021 Paul Amonson
//
// SPDX-License-Identifier: Apache-2.0
//
plugins {
    id 'idea'
    id 'jacoco'
    id 'groovy'
    id 'application'
}

repositories {
    mavenCentral()
}

String gitVersionString() {
    def origin = "main"
    if(System.properties.containsKey("OfficialBuild"))
        origin = "origin/main"
    def stdout = new ByteArrayOutputStream()
    def stderr = new ByteArrayOutputStream()
    exec {
        ignoreExitValue = true
        standardOutput = stdout
        errorOutput = stderr
        commandLine "git", "--git-dir=${rootProject.projectDir}/.git", "describe", origin, "--tags"
    }
    String localVersion = stdout.toString().trim()
    return (localVersion == null || localVersion.isEmpty())?'0.7.0':localVersion
}

dependencies {
    implementation group: 'info.picocli', name: 'picocli', version: '4.6.2'
    annotationProcessor 'info.picocli:picocli-codegen:4.6.2'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.8'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.14.1'

    testImplementation 'org.codehaus.groovy:groovy:3.0.8'
    testImplementation 'org.spockframework:spock-core:2.0-groovy-3.0'
    testImplementation 'junit:junit:4.13.2'
}

project.version = gitVersionString()
project.mainClassName = 'backups.Backup'
application.mainClass = project.mainClassName

test {
    useJUnitPlatform()
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
    manifest {
        attributes("Main-Class": project.mainClassName, "Implementation-Title": project.name,
                "Implementation-Version": project.version, "Multi-Release": true)
    }
}
